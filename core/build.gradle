apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        consumerProguardFile 'proguard-rules.pro'
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -fexceptions"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

/**
 * Since we cant add our path to the java.library.path (.so/dylib lookup places)
 * because gradle will eventually overwrite them with the defaults, we add it
 * to one of those our lib and then delete it
 */
task setupJNILibrary() {
    doLast {
        def extension = org.apache.tools.ant.taskdefs.condition.Os.isFamily(
            org.apache.tools.ant.taskdefs.condition.Os.FAMILY_MAC) ? 'dylib' : 'so'
        def soFilePath = "./build/core/src/main/cpp/libsecure-keys.${extension}";
        File inputFile = new File(soFilePath)
        if (inputFile.exists()) {
            def possiblePlaces = System.properties.'java.library.path'.tokenize(':')
            possiblePlaces.each { dest ->
                File destFile = new File("${dest}/libsecure-keys.${extension}")

                try {
                    if (destFile.createNewFile() || destFile.exists()) {
                        destFile << inputFile.bytes
                        println destFile.absolutePath
                    }
                } catch (IOException ioEx) {
                    // Silent. The dir is probably protected for write access
                }
            }
        } else {
            throw new GradleException(".${extension} file not found. Please run from the root-project folder:\n" +
                "1. mkdir build && cd build\n" +
                "2. cmake ..\n" +
                "3. make\n" +
                "If you have already done this. Then the classloader paths are all write protected, please copy this file on your own to one of this places.\n" +
                "file: ./build/core/src/main/cpp/libsecure-keys.${extension}\n" +
                "places: ${System.properties.'java.library.path'}")
        }
    }
}

task teardownJNILibrary() {
    doLast {
        def extension = org.apache.tools.ant.taskdefs.condition.Os.isFamily(
            org.apache.tools.ant.taskdefs.condition.Os.FAMILY_MAC) ? 'dylib' : 'so'
        def possiblePlaces = System.properties.'java.library.path'.tokenize(':')
        for (def place : possiblePlaces) {
            File file = new File("${place}/libsecure-keys.${extension}")
            if (file.exists()) {
                file.delete()
            }
        }
    }
}

afterEvaluate {
    android.buildTypes.all { buildType ->
        def buildTypeTask = tasks["compile${buildType.name.capitalize()}UnitTestSources"]
        buildTypeTask.dependsOn project.tasks.setupJNILibrary
        project.tasks.teardownJNILibrary.shouldRunAfter buildTypeTask
    }
}

dependencies {
    compile project(path:':annotation')

    compile support.annotations

    testCompile testing.junit
    testCompile testing.googleTruth
    testCompile testing.googleTesting
    testAnnotationProcessor project(path:':processor')
}
