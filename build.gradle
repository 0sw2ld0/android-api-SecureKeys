apply plugin: 'com.saantiaguilera.gradle.publish.helper'

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/saantiaguilera/maven/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'

        classpath "com.saantiaguilera.gradle.publish.helper:core:2.0.1"

        classpath("com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3")
        classpath("com.github.dcendents:android-maven-gradle-plugin:1.5")
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}

publishGlobalConfigurations {
    groupId = project.groupId
    versionName = libraryVersion

    url = "https://github.com/saantiaguilera/android-api-SecureKeys"

    bintrayUser = System.getenv('BINTRAY_USER')
    bintrayApiKey = System.getenv('BINTRAY_KEY')

    licenseUrl = "http://www.opensource.org/licenses/BSD-3-Clause"
    licenseName = "The 3-Clause BSD License"
}

apply from: 'dependencies.gradle'

configure(subprojects.findAll { it.name != 'testapp' }) { subproject ->
    def androidLibs = ['core']
    def javaLibs = ['annotation', 'processor']

    if (androidLibs.contains(subproject.name)) {
        apply plugin: 'com.android.library'

        android {
            compileSdkVersion project.targetApiLevel.toInteger()
            buildToolsVersion project.androidBuildToolsVersion

            defaultConfig {
                minSdkVersion project.minApiLevel
                targetSdkVersion project.targetApiLevel
                versionName libraryVersion
                consumerProguardFile 'proguard-rules.pro'
            }
        }
    }

    if (javaLibs.contains(subproject.name)) {
        apply plugin: 'java'

        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile testing.junit
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    version = '3.5.1'
    distributionType = Wrapper.DistributionType.ALL
}